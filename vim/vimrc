".vimrc
"              _                                                                                    
"      __   __(_)_ __ ___  _ __  ___                                                                
" _____\ \ / /| | '_ ` _ \| / _|/ __|                                                               
"|_____|\ V / | | | | | | |  / | |__                                                                
"        \_/  |_|_| |_| |_|_|   \___|                                                               
"
" Author: Marc Ziani de Ferranti
"
"
" STARTING CONFIG //////////////////////////////////////////////////////////////////////////////////
"
filetype off
call pathogen#infect()                            " Load Pathogen
call pathogen#helptags()
filetype plugin indent on                         " Load file type plugins + indentation
set nocompatible                                  " Choose no compatibility with legacy vi
set cpoptions+=J                                  " Compatability options
syntax on                                         " Turn on syntax highlighting
set mouse=a                                       " Automatically enable mouse usage

au FileType python source <rope-vim>
"
" DEFAULT OPTIONS //////////////////////////////////////////////////////////////////////////////////

set encoding=utf-8                                " Sets Vim's character encoding
set modelines=0                                   " Fixes security exploits
set showmode                                      " Displays current editing mode in status line
set showcmd                                       " Displays incomplete commands in status line
set hidden                                        " Buffer becomes hidden when abandoned
set visualbell                                    " Uses a visual que instead of audio
set cursorline                                    " Highlights current cursor position's line
set ttyfast                                       " Fast terminal connection
set ruler                                         " Shows position in current file
set backspace=indent,eol,start                    " Allows backspace in insert mode
set shell=/bin/bash                               " Sets Vim's terminal environment

" UNDO, BACKUPS & HISTORY //////////////////////////////////////////////////////////////////////////

set undodir=~/dotfiles/vim/tmp/undo//             " Undo file location
set undofile                                      " Enable undo file
set backupdir=~/dotfiles/vim/tmp/backup//         " Backup file location
set backup                                        " Enable backup file
set directory=~/dotfiles/vim/tmp/swap//           " Swap file location
set noswapfile                                    " Disables swp file

set history=1000                                  " Store lots of :cmdline history
if exists("&undoreload")
  set undoreload=10000
endif

" LEADER ///////////////////////////////////////////////////////////////////////////////////////////

let mapleader = ","
let maplocalleader = "\\"

" LINE NUMBERING ///////////////////////////////////////////////////////////////////////////////////

set number                                        " Line numbers on
set numberwidth=4                                 " Fixed coloumn width
set relativenumber                                " Set line numbers to relative positioning

" INDENTATION  /////////////////////////////////////////////////////////////////////////////////////

filetype indent on                                " Activates indenting for files
set autoindent                                    " Use current indent on subsaquent lines
set smartindent                                   " Indents lines after appropriate braces etc
set smarttab                                      " Inserts blank space according to shiftwidth

set tabstop=2                                     " Number of <space>s a <tab> inserts
set shiftwidth=2                                  " Number of <space>s a '>>' inserts
set softtabstop=2                                 " Number of <space>s inserted when editing
set expandtab                                     " <tab>s are replaced with <space> characters

" IndentLine plugin config
let g:indentLine_color_term = 239
let g:indentLine_char = ':'
let g:indentLine_color_gui = '#444444'

" WRAPPING /////////////////////////////////////////////////////////////////////////////////////////

set wrap                                          " Wraps lines that are too long for the window
set textwidth=100                                 " Set character width of splitter
set formatoptions=qrn1
if exists("&colorcolumn")
  set colorcolumn=+1
endif

" SCROLLING ////////////////////////////////////////////////////////////////////////////////////////

set scrolloff=3                                   " Begin scroll x lines from top / bottom
set sidescroll=1                                  " Minimum number of columns to scroll horizontally
set sidescrolloff=10

" INVISIBLE CHARACTERS /////////////////////////////////////////////////////////////////////////////

set list                                          " Display invisible characters
set listchars=tab:▸\ ,eol:¬,extends:❯,precedes:❮  " Set invis charactes for tab, eol, extm precd
set showbreak=↪                                   " Invisible character to indicate wrapped line
" Characters to fill the statuslines and vertical separators
set fillchars=diff:\ 

set autowriteall                                  " Write the file, if modified on various actions
set shiftround
set autoread                                      "Reload files changed outside vim
set dictionary=/usr/share/dict/words

" SPLITS ////////////////////////////////////////////////////////////////////////////////////////////

set splitbelow                                    " New horizontal split window is below current
set splitright                                    " New vertical split window is on the right

" FOLDS ////////////////////////////////////////////////////////////////////////////////////////////

set foldcolumn=1
set columns=150
set foldmethod=indent                             "fold based on indent
set foldnestmax=3                                 "deepest fold is 3 levels
set nofoldenable                                  "dont fold by default

" MISC /////////////////////////////////////////////////////////////////////////////////////////////

set lazyredraw                                    " Screen is not redrawn while using macros

set laststatus=2                                  " Show the statusline

set ttimeout
set notimeout
set nottimeout
set timeout timeoutlen=1000 ttimeoutlen=100

set matchtime=3                                   " Tenths of a second to show matching paren

" WILDMENU /////////////////////////////////////////////////////////////////////////////////////////

set wildmenu
set wildmode=list:longest

set wildignore+=.hg,.git,.svn                    " Version control
set wildignore+=*.aux,*.out,*.toc                " LaTeX intermediate files
set wildignore+=*.jpg,*.bmp,*.gif,*.png,*.jpeg   " binary images
set wildignore+=*.luac                           " Lua byte code
set wildignore+=*.o,*.obj,*.exe,*.dll,*.manifest " compiled object files
set wildignore+=*.pyc                            " Python byte code
set wildignore+=*.spl                            " compiled spelling word lists
set wildignore+=*.sw?                            " Vim swap files
set wildignore+=*.DS_Store?                      " OSX bullshit

set backupskip=/tmp/*,/private/tmp/*"             " Make Vim able to edit crontab files again.

au FocusLost * :wa                                " Save when losing focus

" Resize splits when the window is resized
au VimResized * exe "normal! \<c-w>="

" COLOUR SCHEME ////////////////////////////////////////////////////////////////////////////////////

set background=dark                               " Dark background / color scheme
colorscheme molokai

match ErrorMsg '^\(<\|=\|>\)\{7\}\([^=].\+\)\?$'  " Highlight VCS conflict markers
let g:Powerline_symbols = 'fancy'                 " Powerline

" ABBREVIATIONS ////////////////////////////////////////////////////////////////////////////////////


" SEARCHING ////////////////////////////////////////////////////////////////////////////////////////

set ignorecase                                    " Case insensitive serarching
set smartcase                                     " Case insensitive serarching
set incsearch                                     " Find the next match as we type the search
set showmatch
set hlsearch                                      " Hilight searches by default

" Use sane regexes.
nnoremap / /\v
vnoremap / /\v

" Clear current search
noremap <leader><space> :noh<cr>:call clearmatches()<cr>

" Smart % jumping
runtime macros/matchit.vim
" % Matching with tab key in command mode
map <tab> %

" Don't move on *
nnoremap * *<c-o>

" VISUAL MODE //////////////////////////////////////////////////////////////////////////////////////
"
set virtualedit+=block


" NAVIGATION ///////////////////////////////////////////////////////////////////////////////////////

" Disable arrow keys in Command Mode
nnoremap <up> <nop>
nnoremap <down> <nop>
nnoremap <left> <nop>
nnoremap <right> <nop>
" Disable arrow keys in Insert Mode
inoremap <up> <nop>
inoremap <down> <nop>
inoremap <left> <nop>
inoremap <right> <nop>

" Up and Down work like you expect
noremap j gj
noremap k gk

" Easy buffer navigation
noremap <C-h>  <C-w>h
noremap <C-j>  <C-w>j
noremap <C-k>  <C-w>k
noremap <C-l>  <C-w>l

" Faster Esc
inoremap jj <ESC>
inoremap jk <esc>

" Highlight word
nnoremap <silent> <leader>hh :execute 'match InterestingWord1 /\<<c-r><c-w>\>/'<cr>
nnoremap <silent> <leader>h1 :execute 'match InterestingWord1 /\<<c-r><c-w>\>/'<cr>
nnoremap <silent> <leader>h2 :execute '2match InterestingWord2 /\<<c-r><c-w>\>/'<cr>
nnoremap <silent> <leader>h3 :execute '3match InterestingWord3 /\<<c-r><c-w>\>/'<cr>

" Visual Mode */# from Scrooloose {{{
function! s:VSetSearch()
  let temp = @@
  norm! gvy
  let @/ = '\V' . substitute(escape(@@, '\'), '\n', '\\n', 'g')
  let @@ = temp
endfunction

vnoremap * :<C-u>call <SID>VSetSearch()<CR>//<CR><c-o>
vnoremap # :<C-u>call <SID>VSetSearch()<CR>??<CR><c-o>


" CSS & LESSCSS ////////////////////////////////////////////////////////////////////////////////////

augroup ft_css
  au!

  au BufNewFile,BufRead *.less setlocal filetype=less

  au Filetype less,css setlocal foldmethod=marker
  au Filetype less,css setlocal foldmarker={,}
  au Filetype less,css setlocal omnifunc=csscomplete#CompleteCSS
  au Filetype less,css setlocal iskeyword+=-

  " Use <leader>S to sort properties.  Turns this:
  "
  "     p {
  "         width: 200px;
  "         height: 100px;
  "         background: red;
  "
  "         ...
  "     }
  "
  " into this:

  "     p {
  "         background: red;
  "         height: 100px;
  "         width: 200px;
  "
  "         ...
  "     }
  au BufNewFile,BufRead *.less,*.css nnoremap <buffer> <localleader>S ?{<CR>jV/\v^\s*\}?$<CR>k:sort<CR>:noh<CR>

  " Make {<cr> insert a pair of brackets in such a way that the cursor is correctly
  " positioned inside of them AND the following code doesn't get unfolded.
  au BufNewFile,BufRead *.less,*.css inoremap <buffer> {<cr> {}<left><cr><space><space><space><space>.<cr><esc>kA<bs>
augroup END

" SCSS /////////////////////////////////////////////////////////////////////////////////////////////
"
au BufRead,BufNewFile *.scss set filetype=scss

" DJANGO ///////////////////////////////////////////////////////////////////////////////////////////

augroup ft_django
  au!

  au BufNewFile,BufRead urls.py           setlocal nowrap
  au BufNewFile,BufRead urls.py           normal! zR
  au BufNewFile,BufRead dashboard.py      normal! zR
  au BufNewFile,BufRead local_settings.py normal! zR

  au BufNewFile,BufRead admin.py     setlocal filetype=python.django
  au BufNewFile,BufRead urls.py      setlocal filetype=python.django
  au BufNewFile,BufRead models.py    setlocal filetype=python.django
  au BufNewFile,BufRead views.py     setlocal filetype=python.django
  au BufNewFile,BufRead settings.py  setlocal filetype=python.django
  au BufNewFile,BufRead settings.py  setlocal foldmethod=marker
  au BufNewFile,BufRead forms.py     setlocal filetype=python.django
  au BufNewFile,BufRead common_settings.py  setlocal filetype=python.django
  au BufNewFile,BufRead common_settings.py  setlocal foldmethod=marker
augroup END

" }}}

" HTML & HTMLDJANGO ////////////////////////////////////////////////////////////////////////////////

au BufNewFile,BufRead *.j2 setlocal filetype=htmljinja

augroup ft_html
  au!

  " au BufNewFile,BufRead *.html setlocal filetype=htmljinja
  au FileType html,jinja,htmldjango setlocal foldmethod=manual

  " Use <localleader>f to fold the current tag.
  au FileType html,jinja,htmldjango nnoremap <buffer> <localleader>f Vatzf

  " Use Shift-Return to turn this:
  "     <tag>|</tag>
  "
  " into this:
  "     <tag>
  "         |
  "     </tag>
  au FileType html,jinja,htmldjango nnoremap <buffer> <s-cr> vit<esc>a<cr><esc>vito<esc>i<cr><esc>

  " Smarter pasting
  au FileType html,jinja,htmldjango nnoremap <buffer> p :<C-U>YRPaste 'p'<CR>v`]=`]
  au FileType html,jinja,htmldjango nnoremap <buffer> P :<C-U>YRPaste 'P'<CR>v`]=`]
  au FileType html,jinja,htmldjango nnoremap <buffer> π :<C-U>YRPaste 'p'<CR>
  au FileType html,jinja,htmldjango nnoremap <buffer> ∏ :<C-U>YRPaste 'P'<CR>

  " Django tags
  au FileType jinja,htmldjango inoremap <buffer> <c-t> {%<space><space>%}<left><left><left>

  " Django variables
  au FileType jinja,htmldjango inoremap <buffer> <c-f> {{<space><space>}}<left><left><left>
augroup END

" JAVASCRIPT ///////////////////////////////////////////////////////////////////////////////////////

augroup ft_javascript
  au!

  au FileType javascript setlocal foldmethod=marker
  au FileType javascript setlocal foldmarker={,}
augroup END

" MARKDOWN /////////////////////////////////////////////////////////////////////////////////////////

augroup ft_markdown
    au!

    au BufNewFile,BufRead *.m*down setlocal filetype=markdown foldlevel=20

    " Use <localleader>1/2/3 to add headings.
    au Filetype markdown nnoremap <buffer> <localleader>1 yypVr=
    au Filetype markdown nnoremap <buffer> <localleader>2 yypVr-
    au Filetype markdown nnoremap <buffer> <localleader>3 I### <ESC>
augroup END

" PYTHON ///////////////////////////////////////////////////////////////////////////////////////////

augroup ft_python
    au!

    au Filetype python noremap  <buffer> <localleader>rr :RopeRename<CR>
    au Filetype python vnoremap <buffer> <localleader>rm :RopeExtractMethod<CR>
    au Filetype python noremap  <buffer> <localleader>ri :RopeOrganizeImports<CR>

    au FileType python setlocal omnifunc=pythoncomplete#Complete
    au FileType python setlocal define=^\s*\\(def\\\\|class\\)
    au FileType python compiler nose
    au FileType man nnoremap <buffer> <cr> :q<cr>
augroup END

" QUICKFIX /////////////////////////////////////////////////////////////////////////////////////////

augroup ft_quickfix
    au!
    au Filetype qf setlocal colorcolumn=0 nolist nocursorline nowrap
augroup END

" RESTRUCTURED TEXT ////////////////////////////////////////////////////////////////////////////////

augroup ft_rest
  au!
  au Filetype rst nnoremap <buffer> <localleader>1 yypVr=
  au Filetype rst nnoremap <buffer> <localleader>2 yypVr-
  au Filetype rst nnoremap <buffer> <localleader>3 yypVr~
  au Filetype rst nnoremap <buffer> <localleader>4 yypVr`
augroup END

" RUBY /////////////////////////////////////////////////////////////////////////////////////////////

augroup ft_ruby
  au!
  au Filetype ruby setlocal foldmethod=syntax
augroup END

" MARCZIANI.COM ////////////////////////////////////////////////////////////////////////////////////

augroup ft_marczianicom
  au!
  au BufRead,BufNewFile */marcziani/content/blog/20*/*/*.html set ft=markdown foldlevel=20
  au BufRead,BufNewFile */marcziani/content/projects/*.html set ft=markdown
augroup END

" QUICK FILE EDITING  //////////////////////////////////////////////////////////////////////////////

nnoremap <leader>ev <C-w>s<C-w>j<C-w>L:e ~/dotfiles/vim/vimrc<cr>
nnoremap <leader>ez <C-w>s<C-w>j<C-w>L:e ~/lib/dotfiles/zsh<cr>4j

" Auto re-load vimrc on save
augroup reload_vimrc " {
    autocmd!
    autocmd BufWritePost $MYVIMRC source $MYVIMRC
augroup END " }

" MISC MAPPINGS ////////////////////////////////////////////////////////////////////////////////////

" Search/Replace current word with checking
nnoremap <Leader>s :%s/<c-r><c-w>/<c-r><c-w>/gc<Left><Left><Left>
vnoremap <Leader>s "hy:%s/\%V/g<Left><Left>
vnoremap <Leader>S "hy:%s/<C-r>h//gc<left><left><left>

"This allows for change paste motion cp{motion}
nmap <silent> cp :set opfunc=ChangePaste<CR>g@
function! ChangePaste(type, ...)
    silent exe "normal! `[v`]\"_c"
    silent exe "normal! p"
endfunction

" Change case
noremap <C-u> gUiw
inoremap <C-u><esc> gUiwea

" Substitute
nnoremap <leader>s :%s//<left>

" Formatting, TextMate-style
nnoremap Q gqip

" Easier linewise reselection
nnoremap <leader>v V`]
noremap <leader>v V`]

" Preview Files
nnoremap <leader>p :w<cr>:Hammer<cr>

" HTML tag closing
inoremap <C-_> <Space><BS><Esc>:call InsertCloseTag()<cr>a

" New line and indentation after braces
inoremap {<cr> {<cr>}<c-o>O
inoremap [<cr> [<cr>]<c-o>O
inoremap (<cr> (<cr>)<c-o>O

" Align text
nnoremap <leader>Al :left<cr>
nnoremap <leader>Ac :center<cr>
nnoremap <leader>Ar :right<cr>
vnoremap <leader>Al :left<cr>
vnoremap <leader>Ac :center<cr>
vnoremap <leader>Ar :right<cr>


" Cmdheight switching
nnoremap <leader>1 :set cmdheight=1<cr>
nnoremap <leader>2 :set cmdheight=2<cr>

" Replaste
nnoremap <D-p> "_ddPV`]=

" Marks and Quotes
noremap ' `
noremap æ '
noremap ` <C-^>

" Calculator
inoremap <C-B> <C-O>yiW<End>=<C-R>=<C-R>0<CR>

" REMAP ANNOYING KEYS ////////////////////////////////////////////////////////////////////////////////////

" Fuck you, help key.
if exists("fuoptions")
  set fuoptions=maxvert,maxhorz
endif
noremap  <F1> :set invfullscreen<CR>
inoremap <F1> <ESC>:set invfullscreen<CR>a

" Fuck you too, manual key.
nnoremap K <nop>

" Stop it, hash key.
inoremap # X<BS>#



" Better Completion
set completeopt=longest,menuone,preview
" inoremap <expr> <CR>  pumvisible() ? "\<C-y>" : "\<C-g>u\<CR>"
" inoremap <expr> <C-p> pumvisible() ? '<C-n>'  : '<C-n><C-r>=pumvisible() ? "\<lt>up>" : ""<CR>'
" inoremap <expr> <C-n> pumvisible() ? '<C-n>'  : '<C-n><C-r>=pumvisible() ? "\<lt>Down>" : ""<CR>'

" Sudo to write
cmap w!! w !sudo tee % >/dev/null

" I suck at typing.
nnoremap <localleader>= ==

" Easy filetype switching {{{
nnoremap _md :set ft=markdown<CR>
nnoremap _hd :set ft=htmldjango<CR>
nnoremap _jt :set ft=htmljinja<CR>
nnoremap _cw :set ft=confluencewiki<CR>
nnoremap _pd :set ft=python.django<CR>
nnoremap _d  :set ft=diff<CR>

" Made D behave
nnoremap D d$

" Keep search matches in the middle of the window.
nnoremap n nzzzv
nnoremap N Nzzzv


" Same when jumping around
nnoremap g; g;zz
nnoremap g, g,zz

" Easier to type, and I never use the default behavior.
noremap H ^
noremap L $

" Heresy
inoremap <c-a> <esc>I
inoremap <c-e> <esc>A

" Open a Quickfix window for the last search.
nnoremap <silent> <leader>/ :execute 'vimgrep /'.@/.'/g %'<CR>:copen<CR>

" Ack for the last search.
nnoremap <silent> <leader>? :execute "Ack! '" . substitute(substitute(substitute(@/, "\\\\<", "\\\\b", ""), "\\\\>", "\\\\b", ""), "\\\\v", "", "") . "'"<CR>

" Fix linewise visual selection of various text objects
nnoremap VV V
nnoremap Vit vitVkoj
nnoremap Vat vatV
nnoremap Vab vabV
nnoremap VaB vaBV

" Error navigation {{{
"
"             Location List     QuickFix Window
"            (e.g. Syntastic)     (e.g. Ack)
"            ----------------------------------
" Next      |     M-k               M-Down     |
" Previous  |     M-l                M-Up      |
"            ----------------------------------
"
nnoremap ˚ :lnext<cr>zvzz
nnoremap ¬ :lprevious<cr>zvzz
inoremap ˚ <esc>:lnext<cr>zvzz
inoremap ¬ <esc>:lprevious<cr>zvzz
nnoremap <m-Down> :cnext<cr>zvzz
nnoremap <m-Up> :cprevious<cr>zvzz
" Toggle paste
set pastetoggle=<F8>

" Handle URL {{{
" Stolen from https://github.com/askedrelic/homedir/blob/master/.vimrc
" OSX only: Open a web-browser with the URL in the current line
function! HandleURI()
    let s:uri = matchstr(getline("."), '[a-z]*:\/\/[^ >,;]*')
    echo s:uri
    if s:uri != ""
        exec "!open \"" . s:uri . "\""
    else
        echo "No URI found in line."
    endif
endfunction
map <leader>u :call HandleURI()<CR>
" }}}

" Quickreturn
inoremap <c-cr> <esc>A<cr>
inoremap <s-cr> <esc>A:<cr>

" Indent Guides {{{
let g:indentguides_state = 0
function! IndentGuides() " {{{
    if g:indentguides_state
        let g:indentguides_state = 0
        2match None
    else
        let g:indentguides_state = 1
        execute '2match IndentGuides /\%(\_^\s*\)\@<=\%(\%'.(0*&sw+1).'v\|\%'.(1*&sw+1).'v\|\%'.(2*&sw+1).'v\|\%'.(3*&sw+1).'v\|\%'.(4*&sw+1).'v\|\%'.(5*&sw+1).'v\|\%'.(6*&sw+1).'v\|\%'.(7*&sw+1).'v\)\s/'
    endif
endfunction " }}}
nnoremap <leader>i :call IndentGuides()<cr>

" }}}
" Insert Mode Completion {{{

inoremap <c-l> <c-x><c-l>
inoremap <c-f> <c-x><c-f>

" PLUGINS - CTRL-P ////////////////////////////////////////////////////////////////////////////////////////
"
let g:ctrlp_dont_split = 'NERD_tree_2'
let g:ctrlp_jump_to_buffer = 0
let g:ctrlp_map = '<leader>,'
let g:ctrlp_working_path_mode = 0
let g:ctrlp_match_window_reversed = 1
let g:ctrlp_split_window = 0
let g:ctrlp_max_height = 20
let g:ctrlp_extensions = ['tag']

let g:ctrlp_prompt_mappings = {
  \ 'PrtSelectMove("j")':   ['<c-j>', '<down>', '<s-tab>'],
  \ 'PrtSelectMove("k")':   ['<c-k>', '<up>', '<tab>'],
  \ 'PrtHistory(-1)':       ['<c-n>'],
  \ 'PrtHistory(1)':        ['<c-p>'],
  \ 'ToggleFocus()':        ['<c-tab>'],
  \ }

let ctrlp_filter_greps = "".
  \ "egrep -iv '\\.(" .
  \ "jar|class|swp|swo|log|so|o|pyc|jpe?g|png|gif|mo|po" .
  \ ")$' | " .
  \ "egrep -v '^(\\./)?(" .
  \ "deploy/|lib/|classes/|libs/|deploy/vendor/|.git/|.hg/|.svn/|.*migrations/|docs/build/" .
  \ ")'"

let g:ctrlp_user_command = ['.git/', 'cd %s && git ls-files --exclude-standard -co']

nnoremap <leader>. :CtrlPTag<cr>}

" PLUGINS - COMMENTARY ///////////////////////////////////////////////////////////////////////////////////////

nmap <leader>c <Plug>CommentaryLine
xmap <leader>c <Plug>Commentary
au FileType htmldjango setlocal commentstring={#\ %s\ #}

" PLUGINS - EASYMOTION ///////////////////////////////////////////////////////////////////////////////////////

let g:EasyMotion_leader_key = '<Leader>'

hi EasyMotionTarget ctermbg=none ctermfg=green
hi EasyMotionShade  ctermbg=none ctermfg=cyan

" PLUGINS - FUGITIVE /////////////////////////////////////////////////////////////////////////////////////////

nnoremap <leader>gd :Gdiff<cr>
nnoremap <leader>gs :Gstatus<cr>
nnoremap <leader>gw :Gwrite<cr>
nnoremap <leader>ga :Gadd<cr>
nnoremap <leader>gb :Gblame<cr>
nnoremap <leader>gco :Gcheckout<cr>
nnoremap <leader>gci :Gcommit<cr>
nnoremap <leader>gm :Gmove<cr>
nnoremap <leader>gr :Gremove<cr>

augroup ft_fugitive
  au!

  au BufNewFile,BufRead .git/index setlocal nolist
augroup END

" HTML5 ////////////////////////////////////////////////////////////////////////////////////////////

let g:event_handler_attributes_complete = 0
let g:rdfa_attributes_complete = 0
let g:microdata_attributes_complete = 0
let g:atia_attributes_complete = 0

" PLUGINS - NERDTREE /////////////////////////////////////////////////////////////////////////////////////////

noremap  <F2> :NERDTreeToggle<cr>
inoremap <F2> <esc>:NERDTreeToggle<cr>

au Filetype nerdtree setlocal nolist

let NERDTreeHighlightCursorline=1
let NERDTreeIgnore=['.vim$', '\~$', '.*\.pyc$', 'pip-log\.txt$', 'whoosh_index', 'xapian_index', '.*.pid', 'monitor.py', '.*-fixtures-.*.json', '.*\.o$', 'db.db']

" }}}
" Pydoc {{{

let g:pydoc_perform_mappings = 0

au FileType python noremap <buffer> <localleader>ds :call ShowPyDoc('<C-R><C-W>', 1)<CR>
au FileType python noremap <buffer> <localleader>dS :call ShowPyDoc('<C-R><C-A>', 1)<CR>

" PLUGINS - SPARKUP //////////////////////////////////////////////////////////////////////////////////////////

let g:sparkupNextMapping = '<c-s>'

" PLUGINS - SYNTASTIC ////////////////////////////////////////////////////////////////////////////////////////

let g:syntastic_enable_signs = 1
let g:syntastic_disabled_filetypes = ['html']
let g:syntastic_stl_format = '[%E{Error 1/%e: line %fe}%B{, }%W{Warning 1/%w: line %fw}]'
let g:syntastic_jsl_conf = '$HOME/.vim/jsl.conf'


" }}}
" Threesome {{{

let g:threesome_leader = "-"

let g:threesome_initial_mode = "grid"

let g:threesome_initial_layout_grid = 1
let g:threesome_initial_layout_loupe = 0
let g:threesome_initial_layout_compare = 0
let g:threesome_initial_layout_path = 0

let g:threesome_initial_diff_grid = 1
let g:threesome_initial_diff_loupe = 0
let g:threesome_initial_diff_compare = 0
let g:threesome_initial_diff_path = 0

let g:threesome_initial_scrollbind_grid = 0
let g:threesome_initial_scrollbind_loupe = 0
let g:threesome_initial_scrollbind_compare = 0
let g:threesome_initial_scrollbind_path = 0

let g:threesome_wrap = "nowrap"

" }}}
" Text objects ---------------------------------------------------------------- {{{

" Shortcut for [] {{{

onoremap id i[
onoremap ad a[
vnoremap id i[
vnoremap ad a[

" }}}
" Next and Last {{{

" Motion for "next/last object". For example, "din(" would go to the next "()" pair
" and delete its contents.

onoremap an :<c-u>call <SID>NextTextObject('a', 'f')<cr>
xnoremap an :<c-u>call <SID>NextTextObject('a', 'f')<cr>
onoremap in :<c-u>call <SID>NextTextObject('i', 'f')<cr>
xnoremap in :<c-u>call <SID>NextTextObject('i', 'f')<cr>

onoremap al :<c-u>call <SID>NextTextObject('a', 'F')<cr>
xnoremap al :<c-u>call <SID>NextTextObject('a', 'F')<cr>
onoremap il :<c-u>call <SID>NextTextObject('i', 'F')<cr>
xnoremap il :<c-u>call <SID>NextTextObject('i', 'F')<cr>

function! s:NextTextObject(motion, dir)
  let c = nr2char(getchar())

  if c ==# "b"
      let c = "("
  elseif c ==# "B"
      let c = "{"
  elseif c ==# "d"
      let c = "["
  endif

  exe "normal! ".a:dir.c."v".a:motion.c
endfunction

" }}}

" }}}
" Error toggles --------------------------------------------------------------- {{{

command! ErrorsToggle call ErrorsToggle()
function! ErrorsToggle() " {{{
  if exists("w:is_error_window")
    unlet w:is_error_window
    exec "q"
  else
    exec "Errors"
    lopen
    let w:is_error_window = 1
  endif
endfunction " }}}

command! -bang -nargs=? QFixToggle call QFixToggle(<bang>0)
function! QFixToggle(forced) " {{{
  if exists("g:qfix_win") && a:forced == 0
    cclose
    unlet g:qfix_win
  else
    copen 10
    let g:qfix_win = bufnr("$")
  endif
endfunction " }}}

nmap <silent> <f3> :ErrorsToggle<cr>
nmap <silent> <f4> :QFixToggle<cr>

" }}}
" Utils ----------------------------------------------------------------------- {{{

function! g:echodammit(msg)
    exec 'echom "----------> ' . a:msg . '"'
endfunction

function! StartUp()
    if 0 == argc()
        NERDTree
    end
endfunction

" MACVIM ///////////////////////////////////////////////////////////////////////////////////////////

if has('gui_running')
    set guifont=Inconsolata-dz\ for\ Powerline:h12

    " Remove all the UI cruft
    set go-=T
    set go-=l
    set go-=L
    set go-=r
    set go-=R

    noremap <c-tab> :tabn<cr>
    noremap <c-s-tab> :tabp<cr>

    if has("gui_macvim")

       " PeepOpen on OS X, Command-T elsewhere.
        macmenu &File.New\ Tab key=<nop>
        map <leader><leader> <Plug>PeepOpen

      "Use the normal HIG movements, except for M-Up/Down
        let macvim_skip_cmd_opt_movement = 1
        no   <D-Left>       <Home>
        no!  <D-Left>       <Home>
        no   <M-Left>       <C-Left>
       no!  <M-Left>       <C-Left>

        no   <D-Right>      <End>
        no!  <D-Right>      <End>
        no   <M-Right>      <C-Right>
        no!  <M-Right>      <C-Right>

       no   <D-Up>         <C-Home>
       ino  <D-Up>         <C-Home>
        imap <M-Up>         <C-o>{

        no   <D-Down>       <C-End>
        ino  <D-Down>       <C-End>
        imap <M-Down>       <C-o>}

        imap <M-BS>         <C-w>
        inoremap <D-BS>     <esc>my0c`y
    else
        map <leader><leader> :CommandT<cr>

        " Dammit, PeepOpen
        map gxxxxx <Plug>PeepOpen
    end

    highlight SpellBad term=underline gui=undercurl guisp=Orange

    " Use a line-drawing char for pretty vertical splits.
    set fillchars+=vert:\│
    highlight VertSplit cterm=none gui=none

    " Different cursors for different modes.
    set guicursor=n-c:block-Cursor-blinkon0
    set guicursor+=v:block-vCursor-blinkon0
    set guicursor+=i-ci:ver20-iCursor


else
    " Command-T if we don't have a GUI.
    " noremap <leader><leader> :CommandT<cr>

    " Dammit, PeepOpen
    map gxxxxx <Plug>PeepOpen
endif


nnoremap <Leader>s :%s/<c-r><c-w>/<c-r><c-w>/gc<Left><Left><Left>
